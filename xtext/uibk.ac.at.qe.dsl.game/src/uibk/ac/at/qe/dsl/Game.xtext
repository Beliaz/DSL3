grammar uibk.ac.at.qe.dsl.Game with org.eclipse.xtext.common.Terminals

generate game "http://www.ac.uibk/at/qe/dsl/Game"

Game:
	scenes+=Scene*
;

Scene:
	'new' name=ID
	'{'
		declartions+=LevelDeclaration*
		definitions+=LevelDefinition*
		'start' '=' startLevel=[LevelDeclaration] ';'
		'final' '=' finalLevel=[LevelDeclaration] ';'
	'}'
;

LevelDeclaration:
	'level' name=ID ';'
;

LevelDefinition:
	name=[LevelDeclaration] '='
	'{'
		description=Description
		persons+=Person*
		objects+=Object*
		actions+=Action_GLOBAL*
		'goal' '=' goal=ID ';'
		('next' '=' next=[LevelDeclaration] ';')?
	'}'
;

Description:
	'description' '=' name=STRING ';'
;

Person:
	name=ID '='
	'{'
		'type' '=' type=ID ';'
		'response' '=' response=STRING ';'
		'position' '=' position=STRING ';'
		'action' '=' action=Action_P ';'
	'}'
;

Object:
	name=ID '='
	'{'
		'type' '=' type=ID ';'
		'position' '=' position=STRING ';'
		'action' '=' action=Action_O ';'
	'}'
;

enum Action_GLOBAL:
	LEAVE='Leave'
;

enum Action_O:
	PICK='Pick' | USE = 'Use'
;

enum Action_P:
	TALK='Talk'
;